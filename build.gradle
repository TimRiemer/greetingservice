buildscript {
    ext {
        kotlinVersion = '1.2.30'
        springBootVersion = '2.0.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'docker'

//group = 'mobi.riemer.testing'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

configurations {
    dockettestCompile.extendsFrom testCompile
    dockertestTestRuntime.extendsFrom testRuntime
}

jar {
    baseName = 'greetingservice'
    version = '0.0.1'
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

sourceSets {
    dockertest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/dockertest/kotlin')
        }
        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
        resources.srcDir file('src/dockertest/resources')
    }
}

task buildDocker(type: Docker, dependsOn: build) {
    push = false
    applicationName = jar.baseName
    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

task testDocker(type: Test, dependsOn: buildDocker) {
    testClassesDir = sourceSets.dockertest.output.classesDir
    classpath = sourceSets.dockertest.runtimeClasspath
}

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2")
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("org.jetbrains.kotlin:kotlin-reflect")
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.9.0'

    dockertestCompile group: 'org.testcontainers', name: 'testcontainers', version: '1.6.0'
    dockertestCompile('org.springframework.boot:spring-boot-starter-test')
    dockertestCompile group: 'org.assertj', name: 'assertj-core', version: '3.9.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}
